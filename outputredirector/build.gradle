/*
 * build.gradle
 * OutputRedirector-Android - Redirect stdout and stderr to file
 *
 * Copyright (c) 2020 Vilius Sutkus'89
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

version = '1.0.3'
group = 'com.viliussutkus89'

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        versionCode 4
        versionName project.version

        archivesBaseName = rootProject.name

        externalNativeBuild.cmake.arguments "-DANDROID_STL=c++_shared"
    }
    externalNativeBuild.cmake {
        path "src/main/cpp/CMakeLists.txt"
        version "3.10.2"
    }
    ndkVersion "22.0.7026061"
}

task sourceJar(type: Jar) {
    into ("", { from android.sourceSets.main.java.srcDirs} )
    into ("", { from file("../LICENSE")})
    into ("cpp", { from fileTree(dir: "src/main/cpp/") } )
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    title = "${rootProject.name} ${project.version} API"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['projectRelease']

    pkg {
        repo = 'maven-repo'
        name = rootProject.name
        desc = 'Redirect stdout and stderr to file'

        websiteUrl = 'https://github.com/ViliusSutkus89/OutputRedirector-Android'
        vcsUrl = 'https://github.com/ViliusSutkus89/OutputRedirector-Android.git'
        issueTrackerUrl = 'https://github.com/ViliusSutkus89/OutputRedirector-Android/issues'

        licenses = ['GPL-3.0-only']
        labels = ['stdout', 'stderr', 'redirect']

        publish = true

        version {
            name = this.version
            released = new Date()
            vcsTag = "v${this.version}"
        }
    }
}

publishing {
    publications {
        projectRelease(MavenPublication) {
            artifactId rootProject.name
            artifact "$buildDir/outputs/aar/${rootProject.name}-release.aar"
            artifact sourceJar
            artifact javadocJar
            pom {
                packaging = 'aar'
                url = 'https://github.com/ViliusSutkus89/OutputRedirector-Android'
                inceptionYear = '2019'
                developers {
                    developer {
                        id = "ViliusSutkus89"
                        name = "Vilius Sutkus"
                        email = "ViliusSutkus89@gmail.com"
                    }
                }
                scm {
                    url = 'https://github.com/ViliusSutkus89/OutputRedirector-Android'
                    connection = 'https://github.com/ViliusSutkus89/OutputRedirector-Android.git'
                }
            }
            pom.withXml {
                def node = asNode()

                def licensesNode = node.appendNode("licenses")
                def thisProjLicense = licensesNode.appendNode('license')

                thisProjLicense.appendNode('name', 'OutputRedirector-Android')
                thisProjLicense.appendNode('url', new File("$projectDir/../LICENSE").getText('UTF-8'))
                thisProjLicense.appendNode('distribution', 'repo')

                def dependenciesNode = node.appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones),
                // adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
}
